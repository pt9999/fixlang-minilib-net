module UrlTest;


import Minilib.Net.URL;
import Minilib.Text.StringEx;
import Minilib.Testing.UnitTest;

test_URL_parse_ok: TestCase;
test_URL_parse_ok = (
    make_table_test("test_URL_parse_ok",
        [
            ("http://example.com/aaa/bbb", ("http", "example.com", "80", "/aaa/bbb")),
            ("https://example.com/", ("https", "example.com", "443", "/")),
            ("localhost:8080", ("http", "localhost", "8080", "/")),
            ("127.0.0.1", ("http", "127.0.0.1", "80", "/")),
        ],
        |(str, (scheme, host, port, path))|
        let expected = URL {
            url: str,
            scheme: scheme,
            host: host,
            port: port,
            path: path
        };
        let actual = URL::parse(str);
        assert_equal("eq", ok $ expected, actual)
    )
);

test_URL_parse_ng: TestCase;
test_URL_parse_ng = (
    make_table_test("test_URL_parse_ng",
        [
            ("", "Invalid host"),
            ("example.com:abc", "Invalid port"),
        ],
        |(str, expected)|
        let res = URL::parse(str);
        assert_equal("eq", err $ expected, res)
    )
);

test_encodeURI: TestCase;
test_encodeURI = (
    make_table_test("test_encodeURI",
        [
            ("", ""),
            ("09AZaz", "09AZaz"),
            ("-_.!~*'()", "-_.!~*'()"),
            (";,/?:@&=+$", ";,/?:@&=+$"),
            ("/:@[`{", "/:@%5B%60%7B"), // edge case of 0-9, A-Z, a-z
            ([1_U8, 31_U8, 32_U8, 127_U8, 128_U8, 255_U8]._unsafe_to_string, "%01%1F%20%7F%80%FF"),
        ],
        |(str, expected)|
        let actual = encodeURI(str);
        assert_equal("eq", expected, actual)
    )
);

test_encodeURIComponent: TestCase;
test_encodeURIComponent = (
    make_table_test("test_encodeURIComponent",
        [
            ("", ""),
            ("09AZaz", "09AZaz"),
            ("-_.!~*'()", "-_.!~*'()"),
            (";,/?:@&=+$", "%3B%2C%2F%3F%3A%40%26%3D%2B%24"),
            ("/:@[`{", "%2F%3A%40%5B%60%7B"), // edge case of 0-9, A-Z, a-z
            ([1_U8, 31_U8, 32_U8, 127_U8, 128_U8, 255_U8]._unsafe_to_string, "%01%1F%20%7F%80%FF"),
        ],
        |(str, expected)|
        let actual = encodeURIComponent(str);
        assert_equal("eq", expected, actual)
    )
);

test_decodeURI: TestCase;
test_decodeURI = (
    make_table_test("test_decodeURI",
        [
            ("", ok $ ""),
            ("09AZaz", ok $ "09AZaz"),
            ("-_.!~*'()", ok $ "-_.!~*'()"),
            ("%2D%5F%2E%21%7E%2A%27%28%29", ok $ "-_.!~*'()"),
            (";/?:@&=+$,#", ok $ ";/?:@&=+$,#"),
            ("%3B%2F%3F%3A%40%26%3D%2B%24%2C%23", ok $ "%3B%2F%3F%3A%40%26%3D%2B%24%2C%23"),       // preserves escaped: ";/?:@&=+$,#"
            ("/:@%5B%60%7B", ok $ "/:@[`{"), // edge case of 0-9, A-Z, a-z
            ("%01%1F%20%7F%80%FF", ok $ [1_U8, 31_U8, 32_U8, 127_U8, 128_U8, 255_U8]._unsafe_to_string),
            ("%", err $ "Percent sign requires two characters"),
            ("%0", err $ "Percent sign requires two characters"),
            ("%0X", err $ "Not a hexadecimal character: 'X'"),
        ],
        |(str, expected)|
        let res = decodeURI(str);
        assert_equal("eq", expected, res)
    )
);

test_decodeURIComponent: TestCase;
test_decodeURIComponent = (
    make_table_test("test_decodeURIComponent",
        [
            ("", ok $ ""),
            ("09AZaz", ok $ "09AZaz"),
            ("-_.!~*'()", ok $ "-_.!~*'()"),
            ("%2D%5F%2E%21%7E%2A%27%28%29", ok $ "-_.!~*'()"),
            (";/?:@&=+$,#", ok $ ";/?:@&=+$,#"),
            ("%3B%2F%3F%3A%40%26%3D%2B%24%2C%23", ok $ ";/?:@&=+$,#"),       // does not preserve escaped: ";/?:@&=+$,#"
            ("%2F%3A%40%5B%60%7B", ok $ "/:@[`{"), // edge case of 0-9, A-Z, a-z
            ("%01%1F%20%7F%80%FF", ok $ [1_U8, 31_U8, 32_U8, 127_U8, 128_U8, 255_U8]._unsafe_to_string),
            ("%", err $ "Percent sign requires two characters"),
            ("%0", err $ "Percent sign requires two characters"),
            ("%0X", err $ "Not a hexadecimal character: 'X'"),
        ],
        |(str, expected)|
        let res = decodeURIComponent(str);
        assert_equal("eq", expected, res)
    )
);

main: IO ();
main = (
    [
        test_URL_parse_ok,
        test_URL_parse_ng,
        test_encodeURI,
        test_decodeURI,
        test_encodeURIComponent,
        test_decodeURIComponent,
        TestCase::empty
    ]
    .run_test_driver
);

